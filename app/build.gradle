apply plugin: 'com.android.application'

android {

    //有些变量实在 gradle.properties 文件中读取的
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/res']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDirs = ['libs']
            res.exclude '*.sh'
            res.exclude '*.rb'
            res.exclude '*.script'
            res.exclude '*.bak'
            resources.exclude { '*.sh' }
        }
    }

    signingConfigs {
        release {
            storeFile file("adorkable.jks")
            storePassword "adorkable"
            keyAlias "adorkable_alias"
            keyPassword "adorkable"
        }
        debug {

        }
    }

    //Java的配置
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries = false
        jumboMode = false
    }

    aaptOptions {
        noCompress 'foo', 'bar'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'TypographyFractions', 'TypographyQuotes'
    }
    /**
     * 设置一些默认属性，其可用属性是buildTypes和ProductFlavors之和
     */
    defaultConfig {
        applicationId "me.febsky.toolbox"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion COMPILE_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        // dex突破65535的限制
        multiDexEnabled true
    }


    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            //生成的APK是否支持调试本地代码
            jniDebuggable false
            //不可调试
            debuggable false
            //启用混淆
            minifyEnabled true
            //对APK包执行ZIP对齐优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为Release_V1.0_wandoujia.apk
                        def fileName = "Release_V${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    /**
     * 多渠道
     */
    productFlavors {
        /**
         * 每个flavor 可以覆盖defaultConfig中的所有的值
         */
        fivetv {
            manifestPlaceholders = [CHANNEL_NAME: "febsky"]
        }
/*
        baidu {
            manifestPlaceholders = [CHANNEL_NAME: "baidu"]
        }
        sanxing {
            manifestPlaceholders = [CHANNEL_NAME: "sanxing"]
        }
        meizu {
            manifestPlaceholders = [CHANNEL_NAME: "meizu"]
        }
       anzhi {
            manifestPlaceholders = [CHANNEL_NAME: "anzhi"]
        }
        qh_360 {
            manifestPlaceholders = [CHANNEL_NAME: "qh_360"]
        }
        huawei {
            manifestPlaceholders = [CHANNEL_NAME: "huawei"]
        }
        jifeng {
            manifestPlaceholders = [CHANNEL_NAME: "jifeng"]
        }
        kaiqi {
            manifestPlaceholders = [CHANNEL_NAME: "kaiqi"]
        }
        liantong {
            manifestPlaceholders = [CHANNEL_NAME: "liantong"]
        }
        lianxiang {
            manifestPlaceholders = [CHANNEL_NAME: "lianxiang"]
        }
        liqu {
            manifestPlaceholders = [CHANNEL_NAME: "liqu"]
        }
        nduo {
            manifestPlaceholders = [CHANNEL_NAME: "nduo"]
        }
        oppo {
            manifestPlaceholders = [CHANNEL_NAME: "oppo"]
        }
        sougou {
            manifestPlaceholders = [CHANNEL_NAME: "sougou"]
        }
        wandoujia {
            manifestPlaceholders = [CHANNEL_NAME: "wandoujia"]
        }
        xiaomi {
            manifestPlaceholders = [CHANNEL_NAME: "xiaomi"]
        }
        yidong {
            manifestPlaceholders = [CHANNEL_NAME: "yidong"]
        }
        anzhuoshichang {
            manifestPlaceholders = [CHANNEL_NAME: "anzhuoshichang"]
        }
        ppzhushou {
            manifestPlaceholders = [CHANNEL_NAME: "ppzhushou"]
        }
        baidu91 {
            manifestPlaceholders = [CHANNEL_NAME: "baidu91"]
        }
        yingyonghui {
            manifestPlaceholders = [CHANNEL_NAME: "yingyonghui"]
        }
        yingyongbao {
            manifestPlaceholders = [CHANNEL_NAME: "yingyongbao"]
        }
        jinli {
            manifestPlaceholders = [CHANNEL_NAME: "jinli"]
        }
        tianyi {
            manifestPlaceholders = [CHANNEL_NAME: "tianyi"]
        }
*/
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':library')


    //编译相关
    compile 'com.android.support:multidex:1.0.1'
    //数据统计和分析
    compile 'com.umeng.analytics:analytics:latest.integration'
    // 编程相关
    compile 'com.google.code.gson:gson:2.4'
    compile 'de.greenrobot:eventbus:2.4.1'
    compile 'com.github.bumptech.glide:glide:3.5.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    //android support
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    //其他
    compile 'com.github.andkulikov:transitions-everywhere:1.4.0'
    //    选择性编译只对baidu这个渠道添加
    //    baiduCompile project(':bDAutoUpdateSDK_LibProject')

}